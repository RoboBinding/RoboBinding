buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'net.sf.proguard:proguard-gradle:4.11'
	}
}

archivesBaseName = 'codegen'

repositories {
    mavenCentral()
}

compileTestJava {
	options.compilerArgs << '-proc:none'
}

ext {
	androidVersion = '4.1.1.4'
}

dependencies {
	compile(project(':framework')) {
		exclude group: 'org.robolectric', module: 'android-all'
	}
	compile "com.helger:jcodemodel:$jcodemodelVersion"
	compile ("com.google.android:android:$androidVersion"){
		exclude group: 'commons-logging', module: 'commons-logging'
		exclude group: 'org.apache.httpcomponents', module: 'httpclient'
		exclude group: 'org.khronos', module: 'opengl-api'
		exclude group: 'xerces', module: 'xmlParserAPIs'
		exclude group: 'org.json', module: 'json'
	}
	compile "org.apache.commons:commons-lang3:$commonsLangVersion"
	
	testCompile 'com.google.testing.compile:compile-testing:0.5'
	testCompile 'commons-io:commons-io:2.4'
}

task proguardJarWithDependencies(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
	ext{
		oldClassifier = jar.classifier;
		jar.classifier = 'with-dependencies'
		outJar = jar.archivePath
		jar.classifier = oldClassifier;
	}
	
	configuration 'proguard-rules.pro'
	injars files(jar.archivePath)
	injars configurations.runtime, filter: '!META-INF/MANIFEST.MF'
	outjars  outJar
	libraryjars files("${System.properties['java.home']}/lib/rt.jar")
}

javadoc {
	source = sourceSets.main.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives proguardJarWithDependencies.outJarFileCollection.singleFile
	
	archives sourcesJar
	archives javadocJar
}

configurations.archives.artifacts.removeAll { it.classifier == 'raw' }

apply plugin: 'signing'
signing {
	required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}
signArchives.dependsOn proguardJarWithDependencies

apply plugin: 'maven'
uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
				authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
			}
			
			snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
				authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
			}
			
			//repository(url: "file://$projectDir/mavenRepo")
			pom.project {
				name 'RoboBinding Code Generation'
				description 'A set of RoboBinding Code Generation tools by utilizing Java annotation processors'
				url 'http://robobinding.org'
				
				scm {
					url 'https://github.com/RoboBinding/RoboBinding'
					connection 'scm:git:git@github.com:RoboBinding/RoboBinding.git'
					developerConnection 'scm:git:git@github.com:RoboBinding/RoboBinding.git'
				}
				
				ciManagement {
					system 'Cloudbees Jenkins'
					url 'https://robobindingacc.ci.cloudbees.com/'
				}

				licenses {
					license {
						name 'Apache License Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
						comments """
						Copyright 2012-2014 Cheng Wei and RoboBinding Contributors
						
						Licensed under the Apache License, Version 2.0 (the "License");
						you may not use this file except in compliance with the License.
						You may obtain a copy of the License at
						
						  http://www.apache.org/licenses/LICENSE-2.0
						
						Unless required by applicable law or agreed to in writing, software
						distributed under the License is distributed on an "AS IS" BASIS,
						WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
						See the License for the specific language governing permissions
						and limitations under the License.
						"""
					}
				}

				developers {
					developer {
						name 'Cheng Wei'
						email 'weicheng112@gmail.com'
						organization = 'RoboBinding Open Source'
						organizationUrl 'http://robobinding.org'
						roles {
							role 'Developer'
						}
					}
					developer {
						name 'RoboBinding Contributors'
						roles {
							role 'Developer'
						}
					}
				}
			}
		}
	}
}

apply plugin: 'eclipse'

eclipse {
	project {
		name = 'RoboBinding-codegen'
		comment = 'The RoboBinding code generator'
		
		natures 'org.springsource.ide.eclipse.gradle.core.nature'
	}
	
	classpath {
		containers 'org.springsource.ide.eclipse.gradle.classpathcontainer'
		file {
			whenMerged { classpath ->
				classpath.entries.removeAll { entry ->
					entry.kind == 'lib'
				}
			}
		}

		file.withXml {
			def node = it.asNode()
			
			node.remove( node.find { it.@path == 'src/test/resources' } )
			node.appendNode('classpathentry excluding="**" kind="src" path="src/test/resources"')
		}
	}
}
  
